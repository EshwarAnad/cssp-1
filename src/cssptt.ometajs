ometa CSSBSParser {

    // Tokens

    ident           = m_ident:x                                                     -> [#ident, x],
    atkeyword       = '@' m_ident:x                                                 -> [#atkeyword, x],
    string          = m_string:x                                                    -> [#string, x],
    hash            = '#' m_name:x                                                  -> [#hash, x],
    number          = m_number:x                                                    -> [#number, x],
    percentage      = m_number:x '%'                                                -> [#percentage, x],
    dimension       = number:x m_ident:y                                            -> [#dimension, x, y],
    cdo             = seq('<!--')                                                   -> [#cdo],
    cdc             = seq('-->')                                                    -> [#cdc],
    s               = ((:x m_w(x)) -> x)+:xx                                        -> [#s, xx.join('')],
    attrselector    = (seq('=') | seq('~=') | seq('^=') | seq('$=') | seq('*=')):x  -> [#attrselector, x],
    delim           = ','                                                           -> [#delim, ','],
    comment         = seq('/*') (~seq('*/') char)*:x seq('*/')                      -> [#comment, x.join('')],

    // Macros

    m_ident         = (char:x m_nmstart(x) -> x) (:y m_nmchar(y) -> y)*:z           -> (x + z.join('')),
    m_name          = ((:x m_nmchar(x)) -> x)+:xx                                   -> xx.join(''),
    m_number        = digit+:x '.' digit+:y                                         -> (x.join('') + '.' + y.join(''))
                    | '.' digit+:x                                                  -> ('.' + x.join(''))
                    | digit+:x                                                      -> x.join(''),
    m_string        = '"' (m_string_nl1 | ~'"' char)*:s '"'                         -> ('"' + s.join('') + '"')
                    | '\'' (m_string_nl2 | ~'\'' char)*:s '\''                      -> ('\'' + s.join('') + '\''),
    m_string_nl1    = ('\n' | '\r' | seq('\\"')):x                                  -> x,
    m_string_nl2    = ('\n' | '\r' | seq('\\\'')):x                                 -> x,

    m_nmstart :x    = ?CSSBSParser._m_nmstart(x),
    m_unicode :x    = ?CSSBSParser._m_unicode(x),
    m_escape :x     = ?CSSBSParser._m_escape(x),
    m_nmchar :x     = ?CSSBSParser._m_nmchar(x),
    m_nl :x         = ?CSSBSParser._m_nl(x),
    m_w :x          = ?CSSBSParser._m_w(x),
    m_comment :x    = ?CSSBSParser._m_comment(x)
}

ometa CSSParser <: CSSBSParser {
    sc              = (s | comment),
    stylesheet      = (cdo | cdc | sc | statement)*:x                                                   -> this.concat([#stylesheet], x),
    statement       = (ruleset | atrule),
    atrulepart      = (sc | any | operator),
    mediaq          = atrulepart*:ap                                                                    -> [#mediaq].concat(ap),
    medias          = sc*:s0 ruleset*:r sc*:s1                                                          -> this.concat([#medias], s0, r, s1),
    atrulem         = seq('@media') mediaq:x '{' medias:y '}'                                           -> [#atrulem, x, y],
    atruleb         = atkeyword:ak atrulepart*:ap block:b                                               -> this.concat([#atruleb, ak], ap, [b]),
    atrules         = atkeyword:ak atrulepart*:ap ';'                                                   -> this.concat([#atrules, ak], ap),
    atrule          = (atrulem | atruleb | atrules),
    blockdecl       = sc*:s0 declaration:x decldelim:y sc*:s1                                           -> this.concat(s0, [x], [y], s1)
                    | sc*:s0 declaration:x sc*:s1                                                       -> this.concat(s0, [x], s1)
                    | sc*:s0 decldelim:x sc*:s1                                                         -> this.concat(s0, [x], s1),
    decldelim       = ';'                                                                               -> [#decldelim],
    block           = '{' blockdecl*:x '}'                                                              -> this.concatContent([#block], x),
    ruleset         = selector*:x block:y                                                               -> this.concat([#ruleset], x, [y]),
    combinator      = ('+' | '>' | '~'):x                                                               -> [#combinator, x],
    attrib          = '[' sc*:s0 ident:x sc*:s1 attrselector:a sc*:s2 (ident | string):y sc*:s3 ']'     -> this.concat([#attrib], s0, [x], s1, [a], s2, [y], s3),
    class           = '.' ident:i                                                                       -> [#class, i],
    pseudoe         = seq('::') ident:x                                                                 -> [#pseudoe, x],
    pseudoc         = ':' (function | ident):x                                                          -> [#pseudoc, x],
    pseudo          = (pseudoe | pseudoc),
    nthf            = ':' seq('nth-') (seq('child') | seq('last-child') | seq('of-type') | seq('last-of-type')),
    nth             = (digit | 'n')+:x                                                                  -> [#nth, x.join('')]
                    | (seq('even') | seq('odd')):x                                                      -> [#nth, x],
    nthselector     = nthf:x '(' (sc | unary | nth)*:y ')'                                              -> [#nthselector, x].concat(y),
    simpleselector  = (nthselector | combinator | attrib | pseudo | any | sc)+:x                        -> this.concatContent([#simpleselector], [x]),
    selector        = (simpleselector | delim | sc)+:x                                                  -> this.concat([#selector], x),
    declaration     = property:x ':' value:y                                                            -> [#declaration, x, y],
    property        = ident:x sc*:s0                                                                    -> this.concat([#property, x], s0),
    important       = '!' sc*:s0 seq('important')                                                       -> [#important].concat(s0),
    unary           = ('-' | '+'):x                                                                     -> [#unary, x],
    operator        = ('/' | ',' | ':'):x                                                               -> [#operator, x],
    uri             = seq('url') '(' sc*:s0 string:x sc*:s1')'                                          -> this.concat([#uri], s0, [x], s1)
                    | seq('url') '(' sc*:s0 (~')' ~m_w char)*:x sc*:s1 ')'                              -> this.concat([#uri], s0, [[#raw, x.join('')]], s1),
    value           = (sc | any | block | atkeyword | operator | important)+:x                          -> this.concat([#value], x),
    function        = ident:x '(' (any | sc)*:y ')'                                                     -> this.concat([#function, x], y),
    braces          = '(' sc*:s0 any*:x sc*:s1 ')'                                                      -> this.concat([#braces, '(', ')'], s0, x, s1)
                    | '[' sc*:s0 any*:x sc*:s1 ']'                                                      -> this.concat([#braces, '[', ']'], s0, x, s1),
    any             = (class | string | percentage | dimension | hash | number | uri | function | ident | unary):x  -> x
                    | braces
}

CSSParser.concatContent = function(x, y) {
    y.forEach(function(e) {
        x = x.concat(e);
    });

    return x;
};

CSSParser.concat = function() {
    var x = [];
    for (var i in arguments) {
        x = x.concat(arguments[i]);
    };

    return x;
};

ometa CSSTransformer {
    anys = any*,
    before = [],
    after = [],

    any = before
        | ident
        | atkeyword
        | string
        | hash
        | number
        | percentage
        | dimension
        | cdo
        | cdc
        | decldelim
        | s
        | attrselector
        | attrib
        | nth
        | nthselector
        | class
        | pseudoe
        | pseudoc
        | delim
        | stylesheet
        | mediaq
        | medias
        | atrulem
        | atruleb
        | atrules
        | block
        | ruleset
        | combinator
        | simpleselector
        | selector
        | declaration
        | property
        | important
        | unary
        | operator
        | braces
        | value
        | comment
        | uri
        | raw
        | function
        | after,

    ident           = ['ident':t :x]                -> [t, x],
    atkeyword       = ['atkeyword':t :x]            -> [t, x],
    string          = ['string':t :x]               -> [t, x],
    hash            = ['hash':t :x]                 -> [t, x],
    number          = ['number':t :x]               -> [t, x],
    percentage      = ['percentage':t :x]           -> [t, x],
    dimension       = ['dimension':t :x :y]         -> [t, x, y],
    cdo             = ['cdo':t]                     -> [t],
    cdc             = ['cdc':t]                     -> [t],
    decldelim       = ['decldelim':t]               -> [t],
    s               = ['s':t :x]                    -> [t, x],
    attrselector    = ['attrselector':t :x]         -> [t, x],
    attrib          = ['attrib':t anys:x]           -> [t].concat(x),
    nth             = ['nth':t :x]                  -> [t, x],
    nthselector     = ['nthselector':t :x anys:y]   -> [t, x].concat(y),
    class           = ['class':t :x]                -> [t, x],
    pseudoe         = ['pseudoe':t :x]              -> [t, x],
    pseudoc         = ['pseudoc':t :x]              -> [t, x],
    delim           = ['delim':t :x]                -> [t, x],
    stylesheet      = ['stylesheet':t anys:x]       -> [t].concat(x),
    mediaq          = ['mediaq':t anys:x]           -> [t].concat(x),
    medias          = ['medias':t anys:x]           -> [t].concat(x),
    atrulem         = ['atrulem':t anys:x]          -> [t].concat(x),
    atruleb         = ['atruleb':t anys:x]          -> [t].concat(x),
    atrules         = ['atrules':t anys:x]          -> [t].concat(x),
    block           = ['block':t anys:x]            -> [t].concat(x),
    ruleset         = ['ruleset':t anys:x]          -> [t].concat(x),
    combinator      = ['combinator':t :x]           -> [t, x],
    simpleselector  = ['simpleselector':t anys:x]   -> [t].concat(x),
    selector        = ['selector':t anys:x]         -> [t].concat(x),
    declaration     = ['declaration':t any:p any:v] -> [t, p, v],
    property        = ['property':t anys:x]         -> [t].concat(x),
    important       = ['important':t anys:x]        -> [t].concat(x),
    unary           = ['unary':t :x]                -> [t, x],
    operator        = ['operator':t :x]             -> [t, x],
    braces          = ['braces':t :x :y anys:z]     -> [t, x, y].concat(z),
    value           = ['value':t anys:x]            -> [t].concat(x),
    comment         = ['comment':t :x]              -> [t, x],
    uri             = ['uri':t anys:x]              -> [t].concat(x),
    raw             = ['raw':t :x]                  -> [t, x],
    function        = ['function':t :x anys:y]      -> [t, x].concat(y)
}

ometa CSSTranslator {
    anys = any*,
    before = [],
    after = [],

    any = before
        | ident
        | atkeyword
        | string
        | hash
        | number
        | percentage
        | dimension
        | cdo
        | cdc
        | decldelim
        | s
        | attrselector
        | attrib
        | nth
        | nthselector
        | class
        | pseudoe
        | pseudoc
        | delim
        | stylesheet
        | mediaq
        | medias
        | atrulem
        | atruleb
        | atrules
        | block
        | ruleset
        | combinator
        | simpleselector
        | selector
        | declaration
        | property
        | important
        | unary
        | operator
        | braces
        | value
        | comment
        | uri
        | raw
        | function
        | after,

    ident           = ['ident' :x]                  -> x,
    atkeyword       = ['atkeyword' :x]              -> ('@' + x),
    string          = ['string' :x]                 -> x,
    hash            = ['hash' :x]                   -> ('#' + x),
    number          = ['number' :x]                 -> x,
    percentage      = ['percentage' :x]             -> (x + '%'),
    dimension       = ['dimension' any:x :y]        -> (x + y),
    cdo             = ['cdo']                       -> '<!--',
    cdc             = ['cdc']                       -> '-->',
    decldelim       = ['decldelim']                 -> ';',
    s               = ['s' :x]                      -> x,
    attrselector    = ['attrselector' :x]           -> x,
    attrib          = ['attrib' anys:x]             -> ('[' + x.join('') + ']'),
    nth             = ['nth' :x]                    -> x,
    nthselector     = ['nthselector' :x anys:y]     -> (':nth-' + x + '(' + y.join('') + ')'),
    class           = ['class' any:x]               -> ('.' + x),
    pseudoe         = ['pseudoe' anys:x]            -> ('::' + x.join('')),
    pseudoc         = ['pseudoc' anys:x]            -> (':' + x.join('')),
    delim           = ['delim' :x]                  -> x,
    stylesheet      = ['stylesheet' anys:x]         -> x.join(''),
    mediaq          = ['mediaq' anys:x]             -> x.join(''),
    medias          = ['medias' anys:x]             -> x.join(''),
    atrulem         = ['atrulem' any:x any:y]       -> ('@media' + x + '{' + y + '}'),
    atruleb         = ['atruleb' anys:x]            -> x.join(''),
    atrules         = ['atrules' anys:x]            -> (x.join('') + ';'),
    block           = ['block' anys:x]              -> ('{' + x.join('') + '}'),
    ruleset         = ['ruleset' anys:x]            -> x.join(''),
    combinator      = ['combinator' :x]             -> x,
    simpleselector  = ['simpleselector' anys:x]     -> x.join(''),
    selector        = ['selector' anys:x]           -> x.join(''),
    declaration     = ['declaration' any:p any:v]   -> (p + ':' + v),
    property        = ['property' anys:x]           -> x.join(''),
    important       = ['important' anys:x]          -> ('!' + x.join('') + 'important'),
    unary           = ['unary' :x]                  -> x,
    operator        = ['operator' :x]               -> x,
    braces          = ['braces' :x :y anys:z]       -> (x + z.join('') + y),
    value           = ['value' anys:x]              -> x.join(''),
    comment         = ['comment' :x]                -> ('/*' + x + '*/'),
    uri             = ['uri' anys:x]                -> ('url(' + x.join('') + ')'),
    raw             = ['raw' :x]                    -> x,
    function        = ['function' any:x anys:y]     -> (x + '(' + y.join('') + ')')
}
